apply plugin: "jacoco"

apply from: "${rootDir}/gradle/jacocoExcludes.gradle"

task jacocoModuleReport(type: JacocoReport, dependsOn: "unitTest") {

    group = "Reporting"
    description = "Jacoco test coverage reports for gradle module."

    reports {
        csv.required = false
        xml.required = true
        html.required = true
    }

    final mainSrc = "${project.projectDir}/src/main/kotlin"

    final androidModuleKotlinFileTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug", excludes: jacocoExcludes)
    final androidModuleJavacFileTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: jacocoExcludes)
    final javaModuleKotlinFileTree = fileTree(dir: "${project.buildDir}/classes/kotlin/main", excludes: jacocoExcludes)
    final javaModuleJavacFileTree = fileTree(dir: "${project.buildDir}/classes/java/main", excludes: jacocoExcludes)

    sourceDirectories.setFrom files([mainSrc])
    classDirectories.setFrom files([
        androidModuleKotlinFileTree,
        androidModuleJavacFileTree,
        javaModuleKotlinFileTree,
        javaModuleJavacFileTree
    ])
    executionData.setFrom files(fileTree(dir: project.buildDir, includes: ["/jacoco/*.exec"]))
}
